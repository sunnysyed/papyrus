apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['library']

    dryRun = false
    override = true

    pkg {
        repo = 'maven'
        name = project.PUBLISH_ARTIFACT_ID
        licenses = ['MIT']
        vcsUrl = 'https://github.com/MrFryingpan/papyrus'
        version {
            name = project.PUBLISH_VERSION
            released = new Date()
            desc = project.PUBLISH_DESC
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        library(MavenPublication) {
            artifact sourcesJar
//            artifact javadocJar

            groupId project.PUBLISH_GROUP_ID
            artifactId project.PUBLISH_ARTIFACT_ID
            version project.PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group == 'Papyrus2-Library') {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', project.PUBLISH_GROUP_ID)
                        dependency.appendNode('artifactId', "papyrus-${it.name}")
                        dependency.appendNode('version', project.PUBLISH_VERSION)
                    } else {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}